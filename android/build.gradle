apply plugin: 'com.android.library'

repositories {
    jcenter()
}

project.group = 'sk.bpositive.bcommon'

dependencies {
    compile files('libs/FlashRuntimeExtensions.jar')
    compile 'com.android.support:support-v4:25.3.1'
    compile 'com.google.android.gms:play-services-base:10.2.6'
    compile 'com.google.android.gms:play-services-basement:10.2.6'
    compile 'com.google.android.gms:play-services-appinvite:10.2.6'
    compile 'com.google.android.gms:play-services-tasks:10.2.6'
    compile 'com.google.android.gms:play-services-gcm:10.2.6'

    compile 'com.google.firebase:firebase-core:10.2.6'
//    compile 'com.google.firebase:firebase-common:10.2.6'
//    compile 'com.google.firebase:firebase-iid:10.2.6'
    compile 'com.google.firebase:firebase-messaging:10.2.6'
    compile 'com.google.firebase:firebase-invites:10.2.6'
}

android {
    compileSdkVersion Integer.parseInt(project.ANDROID_BUILD_SDK_VERSION)
    buildToolsVersion project.ANDROID_BUILD_TOOLS_VERSION

    defaultConfig {
        minSdkVersion Integer.parseInt(project.ANDROID_BUILD_MIN_SDK_VERSION)
        targetSdkVersion Integer.parseInt(project.ANDROID_BUILD_TARGET_SDK_VERSION)
    }

    lintOptions {
        abortOnError false
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            res.srcDirs = ['res']
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

task androidJavadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    // JDK 1.8 is more strict then 1.7. Have JDK 1.8 behave like 1.7 for javadoc generation
    if (org.gradle.internal.jvm.Jvm.current().getJavaVersion() == JavaVersion.VERSION_1_8) {
        options.addStringOption('Xdoclint:none', '-quiet')
    }
}

task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
    classifier = 'javadoc'
    from androidJavadocs.destinationDir
}

task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.sourceFiles
}

artifacts {
    archives androidSourcesJar
    archives androidJavadocsJar
}

afterEvaluate {
    androidJavadocs.classpath += project.android.libraryVariants.toList().first().javaCompile.classpath
}

task copyDeps(type: Copy) {
    from configurations.compile
    into 'build/lib'
}